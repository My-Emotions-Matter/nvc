import &ArtSuite
{dataNodes} = &Data

class NavState extends ApplicationState

  @stateFields
    selectedKey: :about
    keyPath: [] :about
    currentTab:     null
    searchResults:  []
    searchQuery:    null

  selectKey: (key) ->
    @clearCurrentTab()
    {depth} = node = &Data.dataNodes[key]
    if @keyPath[depth] == key

      keyPath = @keyPath.slice 0, depth +
        if @keyPath.length - 1 == depth
          0
        else
          1
    else
      keyPath = new Array depth + 1
      while node?.depth >= 0
        keyPath[node.depth] = node.key
        node = dataNodes[node.parentKey]

    @setState
      selectedKey:  peek keyPath
      keyPath:      keyPath
    keyPath

  clearSelectedKey: ->
    @setState
      selectedKey: null
      keyPath: []

  toggleSearchVisible: ->
    if @currentTab == :search
      @currentTab = null
    else
      @currentTab = :search
      @clearSelectedKey()

  showAffirmations: ->
    @currentTab = :affirmations

  searchNodes = (regexps) ->
    array v, k from dataNodes
      toTest = "" #{k} #{v.description}
      matchedAll = true
      each reg from regexps
        matchedAll = false unless reg.test toTest
      if matchedAll then k

  search: (query) ->
    @searchQuery = query
    @searchResults = if query.length > 1
      compactFlatten searchNodes array term from query.split /\s+/
        ///i #{escapeRegExp term}
    else
      []
