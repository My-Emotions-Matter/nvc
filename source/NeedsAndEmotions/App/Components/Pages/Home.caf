import &StandardImport, &Widgets

class Home extends FluxComponent
  @subscriptions :status.status

  @getter
    overallRoundedStatus: ->
      Math.round(@models.status.average)
      4

  render: ->
    ScrollElement
      :column :childrenCenterCenter
      childrenMargins: 20
      padding: 10

      Element
        size:   300
        NeedStatusBitmap status: @overallRoundedStatus

      TextElement
        StyleProps.largeText
        :textCenterCenter
        size: :childrenSize
        text:
          """
            Needs & Emotions v#{&Package.version}

      Element()

      if @models.status extract average
        Element
          size: w: 300, h: 30
          draw:
            padding:
              right:
                300 *
                  1
                  - @models.status.average / 5
            fill:
              StyleProps.colors[@overallRoundedStatus]
            :reset
            outline: #7

          TextElement
            StyleProps.mediumText
            :childrenSize
            :centerCenter
            fontSize: 14
            text:
              ""
                Average:
                #{} average.toFixed 2

      Element
        :column
        size: w: 300 hch: 1
        PillButton
          text: "Start needs assessment"
          wide: true
          action: -> @models.navState.selectKey OrderedNeedsList[0]

        PillButton
          text: "Today's Note"
          wide: true
          action: -> @models.navState.showNotes()

        PillButton
          text: "Search Needs and Emotions"
          wide: true
          action: -> @models.navState.showSearch()

        PillButton
          text: "Import/Export"
          wide: true
          action: -> @models.navState.showImportExport()

      Element()

      TextElement
        StyleProps.smallText
        :textCenterCenter
        :childrenSize
        text:
          """
            Shane Brinkman-Davis Delamore
            shanebdavis@gmail.com
