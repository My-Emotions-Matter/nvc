import &ArtSuite

class Button extends PointerActionsMixin Component

  render: ->
    {text, subtext, emoji, selected, color, style} = @props

    color = rgbColor switch
      when selected || @pointerIsDown then color || &StyleProps.primaryColor
      when color                      then rgbColor(color).blend :white, .5
      else :transparent

    Element
      size: if style == :tab then ps: 1 else wcw: 1, hch: 1
      cursor:     :pointer
      on:         @buttonHandlers
      clip:       true

      RectangleElement
        inFlow:   false
        color:    color
        radius:   100
        padding: 5
        animators:
          color:  {}
          shadow: duration: .3

      if emoji
        Element
          padding:
            if subtext
              bottom: 12, top: 6
            else
              6

          TextElement
            text:       emoji
            location:   ps: .5
            axis:       :centerCenter
            scale:      (ps, cs) -> ps.div(cs).min()
            layoutMode: :tight
            fontSize:   64

      else
        TextElement
          &StyleProps.textStyle
          size:     cs: 1
          align:    :centerCenter
          text:     text.replace /_/g, ' '
          padding:  18

      emoji && subtext &&
        TextElement
          &StyleProps.textStyle
          if style == :tab
            fontSize: 9
            location: ww: .5, yh: 1, y: -3
          else
            fontSize: 10
            location: ww: .5, yh: 1, y: -8
          size:     cs: 1
          axis:     :bottomCenter
          text:     subtext
