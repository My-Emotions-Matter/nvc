import &StandardImport, &Data

class CategoryButton extends PointerActionsMixin FluxComponent
  @subscriptions
    :navState.keyPath
    status: (props) -> props.key

  action: ->
    @models.navState.selectKey @props.key

  hasAncestorKey = (key, lookFor) ->
    key == lookFor
    ||
      dataNodes[key] extract? parentKey
      && hasAncestorKey parentKey, lookFor

  render: ->
    {key} = @props

    selected = key in @keyPath
    text =     key
    color = rgbColor
      switch
      when key == :needs || dataNodes[key].rootKey == :needs
        StyleProps.colors[@status||3]
        || StyleProps.primaryColor

      when hasAncestorKey key, :painful     then #3399fe
      when hasAncestorKey key, :about       then #ffa9f7
      when hasAncestorKey key, :pleasurable then #ffe838
      else                                       #bbb

    Element
      :childrenSize
      :pointerCursor
      :clip
      on:         @buttonHandlers
      animators: :draw
      draw:
        radius:   100
        padding:  5
        color:
          if selected || @pointerIsDown then color
          else color.blend :white, .5

      TextElement
        StyleProps.textStyle
        :childrenSize
        :textCenterCenter
        padding:  18
        text:     text.replace /_/g, ' '
