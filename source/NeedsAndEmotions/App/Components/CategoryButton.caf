import &StandardImport, &Data

class CategoryButton extends PointerActionsMixin FluxComponent
  @subscriptions
    :navState.keyPath
    status: (props) -> props.key

  action: ->
    @models.navState.selectKey @props.key

  hasAncestorKey = (key, lookFor) ->
    if key == lookFor
      true
    else
      {parentKey} = dataNodes[key]
      if parentKey
        hasAncestorKey parentKey, lookFor
      else
        false


  render: ->
    {key} = @props

    selected = key in @keyPath
    text =     key
    color = rgbColor
      if key == "needs" || dataNodes[key].rootKey == "needs"
        StyleProps.colors[@status||3] || StyleProps.primaryColor

      else if hasAncestorKey key, "painful"
        #3399fe # faac11
      else if hasAncestorKey key, "about"
        #ffa9f7
      else if hasAncestorKey key, "pleasurable"
        #ffe838
      else
        #bbb

    Element
      size:       wcw: 1, hch: 1
      cursor:     :pointer
      on:         @buttonHandlers
      clip:       true

      animators: :draw
      draw:
        radius:   100
        padding:  5
        color:
          if selected || @pointerIsDown then color
          else color.blend :white, .5

      TextElement
        StyleProps.textStyle
        size:     :childrenSize
        align:    :centerCenter
        padding:  18
        text.replace /_/g, ' '
