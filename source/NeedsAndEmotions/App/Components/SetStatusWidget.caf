import &StandardImport, &Data

class SetStatusWidget extends FluxComponent
  @subscriptions
    :viewState.viewState
    status: (props) -> props.key

  @stateFields
    descriptionFocused: false

  @getter
    status:    -> @state.status || 3
    landscape: -> @viewState.deviceSize.aspectRatio > 1

  toggleDescriptionFocused: -> @descriptionFocused = !@descriptionFocused

  updateFromPointerEvent: ({location, target}) ->
    {x, y} = target.currentSize
    @models.status.set
      @props.key
      location.x - y / 2
      / (x - y)
      * 4
      + 1

  renderBitmap: ->
    Element
      size:
        if @landscape
          hh: 1, wh: 1
        else
          ps: 1

      BitmapElement
        key: "#{@status}"
        location: ps: .5
        axis: .5
        size: (ps, cs) ->
          if ps.gt cs
            cs
          else
            cs.mul ps.div(cs).min()

        animators: opacity: from: 0, to: .99
        source: :assets/ + switch @status
          when 1 then :winter.png
          when 2 then :fall.png
          when 3 then :unknown.png
          when 4 then :summer.png
          when 5 then :spring.png

  renderDescription: ->
    {textStyle} = StyleProps
    TextElement
      textStyle
      axis:     x:  .5
      location: xw: .5
      size:     w: 300, h: 6 * textStyle.fontSize * textStyle.leading
      align:    :centerCenter
      text:     dataNodes[@props.key].description
      margin:   bottom: 20

  renderText: ->
    TextElement
      StyleProps.textStyle
      color:  StyleProps.colors[@status]
      text:   StyleProps.statuses[@status]
      margin: 10
      size: :childrenSize

  renderSlider: ->
    Element
      size: size = w: 300, h: 44
      margin: 10
      cursor: :pointer

      on:
        pointerUp:    @updateFromPointerEvent
        pointerMove:  @updateFromPointerEvent

      draw: padding: 15, radius: 10000, color: #0001

      Element
        size: 40

        animators:  :draw :location
        axis:       :centerCenter

        location: yh: .5, x:
          (size.w - size.h) * (@status - 1)
          / 4
          + size.h / 2

        draw:
          :circle
          color:  StyleProps.colors[@status]
          shadow: true
          {} outline: lineWidth: 2 color: :white

  renderLandscape: ->
    Element
      childrenLayout:     :row
      childrenAlignment:  :center

      @renderBitmap()

      Element
        childrenLayout:     :column
        childrenAlignment:  :bottomCenter
        padding: bottom:    10

        @renderDescription()
        @renderText()
        @renderSlider()

  renderPortrait: ->
    Element
      childrenLayout:     :column
      childrenAlignment:  :center
      padding: bottom:    25

      @renderBitmap()
      @renderDescription()
      @renderText()
      @renderSlider()

  render: ->
    Element
      if @viewState.deviceSize
        if @landscape
          @renderLandscape()
        else
          @renderPortrait()
