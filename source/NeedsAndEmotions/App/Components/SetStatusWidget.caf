import &StandardImport, &Data, {} &CategoryButton, &Data/NavigationGraph

class SetStatusWidget extends FluxComponent
  @subscriptions
    :viewState.viewState
    status: (props) -> props.key

  @stateFields
    descriptionFocused: false

  @propFields :key

  @getter
    status:    -> @state.status || 3
    landscape: -> @viewState.deviceSize.aspectRatio > 1

  toggleDescriptionFocused: -> @descriptionFocused = !@descriptionFocused

  updateFromPointerEvent: ({location, target}) ->
    {x, y} = target.currentSize
    @models.status.set
      @props.key
      location.x - y / 2
      / (x - y)
      * 4
      + 1

  renderBitmap: ->
    Element
      :parentHeightSquare if @landscape

      BitmapElement
        key: "#{@status}"
        :centerCenter
        :parentWidthSquare

        animators: opacity: from: 0, to: .99
        source: :assets/ + switch @status
          when 1 then :winter.png
          when 2 then :fall.png
          when 3 then :unknown.png
          when 4 then :summer.png
          when 5 then :spring.png

  renderDescription: ->
    {textStyle} = StyleProps
    TextElement
      textStyle
      :textCenterCenter
      :topCenter
      size:     w: 300, h: 6 * textStyle.fontSize * textStyle.leading
      text:     dataNodes[@props.key].description
      margin:   bottom: 20

  renderText: ->
    TextElement
      StyleProps.textStyle
      :childrenSize
      color:  StyleProps.colors[@status]
      text:   StyleProps.statuses[@status]
      margin: 10

  renderSlider: ->
    Element
      size: size = w: 300, h: 44
      margin: 10
      :pointerCursor

      on:
        pointerUp:    @updateFromPointerEvent
        pointerMove:  @updateFromPointerEvent

      draw: padding: 15, radius: 10000, color: #0001

      Element
        :centerCenter
        size:       40
        animators:  :draw :location
        location: yh: .5, x:
          (size.w - size.h) * (@status - 1)
          / 4
          + size.h / 2

        draw:
          :circle
          color:  StyleProps.colors[@status]
          shadow: true
          {} outline: lineWidth: 2 color: :white

  renderLandscape: ->
    Element
      :row :childrenCenterCenter

      @renderBitmap()

      Element
        :column :childrenBottomCenter
        padding: bottom:    10

        @renderDescription()
        @renderText()
        @renderSlider()

  renderPortrait: ->
    Element
      :column
      :childrenCenterCenter

      @renderBitmap()
      @renderDescription()
      @renderText()
      @renderSlider()

  render: ->

    NavigationGraph[@key] extract? prev, next
    Element
      if @viewState.deviceSize
        if @landscape
          @renderLandscape()
        else
          @renderPortrait()

      Element
        :parentWidthChildrenHeight
        :row
        prev && CategoryButton key: prev
        Element()
        next && CategoryButton key: next
